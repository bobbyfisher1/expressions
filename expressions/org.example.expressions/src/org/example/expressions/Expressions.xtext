grammar org.example.expressions.Expressions with org.eclipse.xtext.common.Terminals

generate expressions "http://www.example.org/expressions/Expressions"

ExpressionsModel:
	elements+=AbstractElement*;

AbstractElement:
	Variable | EvalExpression;

Variable:
	'var' name=ID '=' expression=Expression;

EvalExpression:
	'eval' expression=Expression;

Expression:
//	Atomic ({Plus.left=current} '+' right=Expression)? ; 	//right recursive
//	Atomic ({Plus.left=current} '+' right=Atomic)*; //left recursive
	Atomic (({Plus.left=current} '+' | {Minus.left=current} '-') right=Atomic)*;

Atomic returns Expression:
	'(' Expression ')' //
	| {IntConstant} value=INT //
	| {StringConstant} value=STRING // 
	| {BoolConstant} value=('true' | 'false') // 
	| {VariableRef} variable=[Variable]; //